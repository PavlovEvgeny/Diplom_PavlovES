#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	СформироватьДвижения(); 
			
	РассчитатьОклад(); 
	
	РассчитатьОтпуск();
	
	РассчитатьНДФЛ();
	
	РассчитатьВзаиморасчеты();
			
КонецПроцедуры 

Процедура СформироватьДвижения()
	
	Движения.ВКМ_ОсновныеНачисления.Записывать = Истина;
	Движения.ВКМ_Удержания.Записывать = Истина; 
	
	Для Каждого Строка Из ОсновныеНачисления Цикл
				
		Если ТипЗнч(Строка.ВидРасчета) = Тип("ПланВидовРасчетаСсылка.ВКМ_ОсновныеНачисления") Тогда
			
			Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
			Движение.ПериодРегистрации = НачалоМесяца(Дата);
			Движение.ПериодДействияНачало = Строка.ДатаНачала;
			Движение.ПериодДействияКонец = Строка.ДатаОкончания;
			Движение.ВидРасчета = Строка.ВидРасчета;
			Движение.Сотрудник = Строка.Сотрудник;
			Движение.Подразделение = Строка.Подразделение;
			Движение.График = Строка.ГрафикРаботы;
						
			Если Строка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск Тогда
				
				ПериодРегистрации = НачалоМесяца(Дата);
				НачалоБазовогоПериода = ДобавитьМесяц(ПериодРегистрации, -12);
				КонецБазовогоПериода = ПериодРегистрации - 1;
	
				Движение.БазовыйПериодНачало = НачалоБазовогоПериода;
				Движение.БазовыйПериодКонец = КонецБазовогоПериода;
								
			КонецЕсли;
									
		КонецЕсли;
		
			Движение = Движения.ВКМ_Удержания.Добавить();
			Движение.ПериодРегистрации = НачалоМесяца(Дата);
			Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
			Движение.БазовыйПериодНачало = Строка.ДатаНачала;
			Движение.БазовыйПериодКонец = Строка.ДатаОкончания;
			Движение.Сотрудник = Строка.Сотрудник;
			Движение.Подразделение = Строка.Подразделение;
			Движение.График = Строка.ГрафикРаботы;
								
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать();
	Движения.ВКМ_Удержания.Записать();
						
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИфункции

Процедура РассчитатьОклад()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки,
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.РабочихДнейПериодДействия КАК ДнейПлан,
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.РабочихДнейФактическийПериодДействия КАК ДнейФакт,
		|	ЕСТЬNULL(ВКМ_УсловияОплатыСотрудниковСрезПоследних.Оклад, 0) КАК Оклад
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(Регистратор = &Ссылка
		|	И ВидРасчета = &ВидРасчета) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Период, (Сотрудник, Подразделение) В
		|			(ВЫБРАТЬ
		|				ВКМ_ОсновныеНачисления.Сотрудник,
		|				ВКМ_ОсновныеНачисления.Подразделение
		|			ИЗ
		|				РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
		|			ГДЕ
		|				ВКМ_ОсновныеНачисления.Регистратор = &Ссылка
		|				И ВКМ_ОсновныеНачисления.ВидРасчета = &ВидРасчета)) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
		|		ПО ВКМ_ОсновныеНачисленияДанныеГрафика.Сотрудник = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник
		|		И ВКМ_ОсновныеНачисленияДанныеГрафика.Подразделение = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Подразделение";
	
	Запрос.УстановитьПараметр("ВидРасчета", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", НачалоМесяца(Дата));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки - 1];
		
		Если Выборка.ДнейПлан = 0 Тогда
			Движение.Результат = 0;
		Иначе
			Движение.Результат = (Выборка.Оклад * Выборка.ДнейФакт / Выборка.ДнейПлан);
			Движение.НормаДней = Выборка.ДнейПлан;
			Движение.ФактДней = Выборка.ДнейФакт; 
		КонецЕсли;
				
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать(, Истина);
	
КонецПроцедуры

Процедура РассчитатьОтпуск()

		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
			|	ВКМ_ОсновныеНачисленияДанныеГрафика.РабочихДнейБазовыйПериод КАК БазаДней,
			|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.РезультатБаза, 0) КАК БазаНачислений,
			|	ВКМ_ОсновныеНачисленияДанныеГрафика.КалендарныхДнейФактическийПериодДействия КАК ДнейОтпускаФакт
			|ИЗ
			|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(
			|			Регистратор = &Ссылка
			|				И ВидРасчета = &ВидРасчета) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ОсновныеНачисления(
			|				&Измерения,
			|				&Измерения,
			|				,
			|				Регистратор = &Ссылка
			|					И ВидРасчета = &ВидРасчета) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления
			|		ПО ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки = ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.НомерСтроки";
		
		Запрос.УстановитьПараметр("ВидРасчета", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск);
		
		Измерения = Новый Массив;
		Измерения.Добавить("Сотрудник");
		Измерения.Добавить("Подразделение");
		
		Запрос.УстановитьПараметр("Измерения", Измерения);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Движение = Движения.ВКМ_ОсновныеНачисления[ВыборкаДетальныеЗаписи.НомерСтроки - 1];
		
			Движение.Результат = (ВыборкаДетальныеЗаписи.ДнейОтпускаФакт * ВыборкаДетальныеЗаписи.БазаНачислений / ВыборкаДетальныеЗаписи.БазаДней);
			Движение.БазаНачислений = ВыборкаДетальныеЗаписи.БазаНачислений;
			Движение.ФактДней = ВыборкаДетальныеЗаписи.ДнейОтпускаФакт;
					
		КонецЦикла;
		
		Движения.ВКМ_ОсновныеНачисления.Записать(, Истина);
	
КонецПроцедуры

Процедура РассчитатьНДФЛ()
				
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.РезультатБаза КАК РезультатБаза,
		|	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.Подразделение КАК Подразделение
		|ИЗ
		|	РегистрРасчета.ВКМ_Удержания.БазаВКМ_ОсновныеНачисления(&Измерения, &Измерения,, ВидРасчета = &ВидРасчета
		|	И Регистратор = &Ссылка) КАК ВКМ_УдержанияБазаВКМ_ОсновныеНачисления";
	
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	Измерения.Добавить("Подразделение");
	
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("ВидРасчета", ПланыВидовРасчета.ВКМ_Удержания.НДФЛ);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Движение = Движения.ВКМ_Удержания[ВыборкаДетальныеЗаписи.НомерСтроки - 1];
		
		Движение.Результат = (ВыборкаДетальныеЗаписи.РезультатБаза * 13 / 100);

	КонецЦикла;
	
	Движения.ВКМ_Удержания.Записать(, Истина);
	
КонецПроцедуры

Процедура РассчитатьВзаиморасчеты()
	
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
		
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисления.Сотрудник,
		|	ВКМ_ОсновныеНачисления.Подразделение,
		|	ВКМ_ОсновныеНачисления.Результат
		|ПОМЕСТИТЬ ВТ_Начисления
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
		|ГДЕ
		|	ВКМ_ОсновныеНачисления.Регистратор = &Регистратор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВКМ_ДополнительныеНачисления.Сотрудник,
		|	ВКМ_ДополнительныеНачисления.Подразделение,
		|	ВКМ_ДополнительныеНачисления.Результат
		|ИЗ
		|	РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
		|ГДЕ
		|	ВКМ_ДополнительныеНачисления.Регистратор = &Регистратор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВКМ_Удержания.Сотрудник,
		|	ВКМ_Удержания.Подразделение,
		|	-ВКМ_Удержания.Результат
		|ИЗ
		|	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
		|ГДЕ
		|	ВКМ_Удержания.Регистратор = &Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&ПериодРегистрации КАК ПериодРегистрации,
		|	ВТ_Начисления.Сотрудник КАК Сотрудник,
		|	ВТ_Начисления.Подразделение КАК Подразделение,
		|	СУММА(ВТ_Начисления.Результат) КАК Результат
		|ИЗ
		|	ВТ_Начисления КАК ВТ_Начисления
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Начисления.Сотрудник,
		|	ВТ_Начисления.Подразделение";
	
	Запрос.УстановитьПараметр("ПериодРегистрации", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.ДобавитьПриход();
		Движение.Период = НачалоМесяца(Дата);
		Движение.Сотрудник = ВыборкаДетальныеЗаписи.Сотрудник;
		Движение.Подразделение = ВыборкаДетальныеЗаписи.Подразделение;
		Движение.Сумма = ВыборкаДетальныеЗаписи.Результат;

	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти
