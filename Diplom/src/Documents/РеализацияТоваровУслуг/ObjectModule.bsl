
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		ЗаполнитьНаОснованииЗаказаПокупателя(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	СуммаДокумента = Товары.Итог("Сумма") + Услуги.Итог("Сумма");
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ОбработкаЗаказов.Записывать = Истина;
	Движения.ОстаткиТоваров.Записывать = Истина;
	Движения.ВКМ_ВыставленныеРаботыКлиенту.Записывать = Истина;
	
	Движение = Движения.ОбработкаЗаказов.Добавить();
	Движение.Период = Дата;
	Движение.Контрагент = Контрагент;
	Движение.Договор = Договор;
	Движение.Заказ = Основание;
	Движение.СуммаОтгрузки = СуммаДокумента;

	Для Каждого ТекСтрокаТовары Из Товары Цикл
		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
		Движение.Сумма = ТекСтрокаТовары.Сумма;
		Движение.Количество = ТекСтрокаТовары.Количество;
	КонецЦикла;
	
	//{{Павлов Е.С.: Добавляем заполнение регистра ВКМ_ВыставленныеРаботыКлиенту.
	Для Каждого ТекСтрокаУслуги Из Услуги Цикл
		Движение = Движения.ВКМ_ВыставленныеРаботыКлиенту.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Клиент = Контрагент;
		Движение.Договор = Договор;
		Движение.Номенклатура = ТекСтрокаУслуги.Номенклатура;
		Движение.СуммаОплаты = ТекСтрокаУслуги.Сумма;
	КонецЦикла;
   //}}

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьНаОснованииЗаказаПокупателя(ДанныеЗаполнения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказПокупателя.Организация КАК Организация,
	               |	ЗаказПокупателя.Контрагент КАК Контрагент,
	               |	ЗаказПокупателя.Договор КАК Договор,
	               |	ЗаказПокупателя.СуммаДокумента КАК СуммаДокумента,
	               |	ЗаказПокупателя.Товары.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма
	               |	) КАК Товары,
	               |	ЗаказПокупателя.Услуги.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма
	               |	) КАК Услуги
	               |ИЗ
	               |	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	               |ГДЕ
	               |	ЗаказПокупателя.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	
	ТоварыОснования = Выборка.Товары.Выбрать();
	Пока ТоварыОснования.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Товары.Добавить(), ТоварыОснования);
	КонецЦикла;
	
	УслугиОснования = Выборка.Услуги.Выбрать();
	Пока ТоварыОснования.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Услуги.Добавить(), УслугиОснования);
	КонецЦикла;
	
	Основание = ДанныеЗаполнения;
	
КонецПроцедуры

//{{Павлов Е.С.: Добавляем процедуру "ВыполнитьАвтозаполнение()" для автоматического заполннения документа.
Процедура ВыполнитьАвтозаполнение() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_НоменклатураАбонентскаяПлата.Значение КАК АбонентскаяПлата,
		|	ВКМ_НоменклатураРаботыСпециалиста.Значение КАК РаботыСпециалиста,
		|	ВКМ_ВыполненныеКлиентуРаботыОбороты.Клиент КАК Клиент,
		|	ВКМ_ВыполненныеКлиентуРаботыОбороты.Договор КАК Договор,
		|	СУММА(ВКМ_ВыполненныеКлиентуРаботыОбороты.Договор.ВКМ_СуммаАбонентскойПлаты) КАК СуммаАбонентскойПлаты,
		|	СУММА(ВКМ_ВыполненныеКлиентуРаботыОбороты.КоличествоЧасовОборот) КАК КоличествоЧасов,
		|	СУММА(ВКМ_ВыполненныеКлиентуРаботыОбороты.СуммаКОплатеОборот) КАК СуммаКОплате
		|ИЗ
		|	Константа.ВКМ_НоменклатураАбонентскаяПлата КАК ВКМ_НоменклатураАбонентскаяПлата,
		|	Константа.ВКМ_НоменклатураРаботыСпециалиста КАК ВКМ_НоменклатураРаботыСпециалиста,
		|	РегистрНакопления.ВКМ_ВыполненныеКлиентуРаботы.Обороты(&НачалоПериода, &КонецПериода, , Договор = &Договор) КАК ВКМ_ВыполненныеКлиентуРаботыОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	ВКМ_НоменклатураАбонентскаяПлата.Значение,
		|	ВКМ_ВыполненныеКлиентуРаботыОбороты.Договор,
		|	ВКМ_НоменклатураРаботыСпециалиста.Значение,
		|	ВКМ_ВыполненныеКлиентуРаботыОбороты.Клиент";
	
	Запрос.УстановитьПараметр("Договор", Договор);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Услуги.Очистить();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Если ВыборкаДетальныеЗаписи.АбонентскаяПлата = Неопределено И ВыборкаДетальныеЗаписи.РаботыСпециалиста = Неопределено Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не заполнены константы номенклатур";
			Сообщение.Сообщить();
		Возврат;

		КонецЕсли;
		
		Если ВыборкаДетальныеЗаписи.СуммаАбонентскойПлаты <> 0 Тогда
			НоваяСтрокаУслуги = Услуги.Добавить();
			НоваяСтрокаУслуги.Номенклатура = ВыборкаДетальныеЗаписи.АбонентскаяПлата;
			НоваяСтрокаУслуги.Сумма = ВыборкаДетальныеЗаписи.СуммаАбонентскойПлаты;
		КонецЕсли;
		 
		//Если Месяц(Дата) = Месяц(ВыборкаДетальныеЗаписи.Период) Тогда
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.СуммаКОплате) Тогда
		 	НоваяСтрокаУслуги = Услуги.Добавить();
			НоваяСтрокаУслуги.Номенклатура = ВыборкаДетальныеЗаписи.РаботыСпециалиста;
			НоваяСтрокаУслуги.Количество = ВыборкаДетальныеЗаписи.КоличествоЧасов;
			НоваяСтрокаУслуги.Сумма = ВыборкаДетальныеЗаписи.СуммаКОплате;
			НоваяСтрокаУслуги.Цена = ВыборкаДетальныеЗаписи.СуммаКОплате / ВыборкаДетальныеЗаписи.КоличествоЧасов;
		 КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры
//}}

#КонецОбласти

#КонецЕсли
